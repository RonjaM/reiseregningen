<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
	creationComplete="initPage()" width="100%" 
	updateComplete="initPage()">
	
	<mx:Script>
		<![CDATA[
			import mx.events.CloseEvent;
			import mx.collections.SortField;
			import mx.collections.Sort;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import no.makingwaves.cust.dss.vo.TravelAccomodationVO;
			import mx.collections.ArrayCollection;
            import mx.core.IFlexDisplayObject;
			import no.makingwaves.cust.dss.model.ModelLocator;
			import no.makingwaves.util.Util;
			import mx.managers.PopUpManager;
			import no.makingwaves.cust.dss.view.specifications.travel_accomodation;
			
			[Bindable]
			public var model : ModelLocator = ModelLocator.getInstance();
			
			[Bindable]
			public var active_btn_edit : Boolean = true;
			
			[Bindable]
			public var active_btn_delete : Boolean = true;

			private var addWindow:IFlexDisplayObject;
				
			private function initPage():void {
				if (grid_accomodations.selectedIndex == -1) {
					this.active_btn_delete = false;
					this.active_btn_edit = false;
				}
				this.initColumnSizes();
			}		
			
			private function getFormattedDateAndTime(date:Date, time:String):String {
				var formattedDate:String = "";
				if (date != null) {
					var dateFormat:String = resourceManager.getString(model.resources.getResourceBundleName(), 'DATE_FORMAT');
					formattedDate = Util.formatDate(date, dateFormat);
					
					if (time != "") 	
						formattedDate += ", kl." + time.substr(0,2) + ":" + time.substr(2,2);
						
				}
				return formattedDate;
			}
			
			public function editSpecification(e:MouseEvent=null):void {
				var popupSpec:travel_accomodation = travel_accomodation(PopUpManager.createPopUp(this, travel_accomodation, true));
				popupSpec.travelaccomodations = TravelAccomodationVO(grid_accomodations.selectedItem);
				popupSpec.addEventListener(CloseEvent.CLOSE, closeWindowHandler);
				
				function closeWindowHandler(e:CloseEvent):void {
					popupSpec.removeEventListener(CloseEvent.CLOSE, closeWindowHandler);
					PopUpManager.removePopUp(popupSpec);
				}
			}
			
			public function deleteSpecification(e:MouseEvent=null):void {
				var travelaccomodations:TravelAccomodationVO = TravelAccomodationVO(grid_accomodations.selectedItem); 
				for (var i:Number = 0; i < model.travelAccomodationList.length; i++) {
					if (model.travelAccomodationList.getItemAt(i) == travelaccomodations) {
						model.travelAccomodationList.removeItemAt(i);
						break;
					}
				}
			}
			
			public function getSelectedStatus():Boolean {
				return Boolean(this.grid_accomodations.selectedIndex != -1);				
			}
			
			private function gridFormatterType(item:TravelAccomodationVO, ref:DataGridColumn):String {
				var type:String = "";
				switch(item.type) {
					case item.TYPE_HOTEL:
						type = resourceManager.getString(model.resources.bundleName, 'accomodation_type_hotel');
						break;
					case item.TYPE_UNATHORIZED:
						type = resourceManager.getString(model.resources.bundleName, 'accomodation_type_unauthorized');
						break;
					case item.TYPE_UNATHORIZED_HOTEL:
						type = resourceManager.getString(model.resources.bundleName, 'accomodation_type_hotel_unauthorized');
						break;
						
				}
				return type;
			}
			
			private function gridFormatterCost(item:TravelAccomodationVO, ref:DataGridColumn):String {
				return item.actual_cost.getCost(); //item.cost.getCost();
			}
			
			private function gridFormatterDate(item:TravelAccomodationVO, ref:DataGridColumn):String {
				var label:String = "";
				label = this.getFormattedDateAndTime(item.fromdate, "");
				label += " - " + this.getFormattedDateAndTime(item.todate, "");
				return label;
			}
			
			private function dateSortCompareFunction(obj1:Object, obj2:Object):int {
				if (obj1.fromdate > obj2.fromdate) {
					return -1;
				} else if (obj1.fromdate < obj2.fromdate) {
					return 1;
				}
				return 0;
			}
			
			private function initColumnSizes():void {
				var timer:Timer = new Timer(500,1);
				timer.addEventListener(TimerEvent.TIMER_COMPLETE, setColumnSizes);
				timer.start();				
			}
			private function setColumnSizes(e:TimerEvent):void {
				try {
					this.grid_accomodations.resizableColumns = true;
					this.column1.width = getColumnSize(15);
					this.column2.width = getColumnSize(20);		
					this.column3.width = getColumnSize(50);
					this.column4.width = getColumnSize(15);
				} catch (e:Error) { trace("accomodation.resumeList.setColumnSizes: " + e.message); }		
			}
			
			private function getColumnSize(percent:Number):Number {
				return (grid_accomodations.width * percent) / 100;
			}
			
			private function gridSelectionChanged(e:ListEvent):void {
				if (grid_accomodations.selectedIndex != -1) {
					this.active_btn_delete = true;
					this.active_btn_edit = true;
				} else {
					this.active_btn_delete = false;
					this.active_btn_edit = false;
				}
			}
			
		]]>
	</mx:Script>
		
	<mx:DataGrid id="grid_accomodations" tabIndex="267" rowCount="{((model.travelAccomodationList.length > 0) ? model.travelAccomodationList.length : 1)}" width="100%" height="100%"
				scaleX="{((model.travelAccomodationList.length > 0) ? 1 : 0)}" 
				scaleY="{((model.travelAccomodationList.length > 0) ? 1 : 0)}" 
				dataProvider="{model.travelAccomodationList}" addedToStage="{initColumnSizes()}"
			    change="this.gridSelectionChanged(event)"
			    doubleClick="{this.editSpecification(event)}">
				
        <mx:columns>
	        	<mx:DataGridColumn id="column1" headerText="{resourceManager.getString(model.resources.bundleName, 'type_listlabel')}"
	        					    sortable="false" activate="{column1.width=getColumnSize(15)}"	
	        						labelFunction="gridFormatterType" />
	            <mx:DataGridColumn id="column2" headerText="{resourceManager.getString(model.resources.bundleName, 'date_listlabel')}"
	            					sortable="true" sortCompareFunction="dateSortCompareFunction" activate="{column2.width=getColumnSize(20)}"
	            					labelFunction="gridFormatterDate" />
	            <mx:DataGridColumn id="column3" headerText="{resourceManager.getString(model.resources.bundleName, 'location_listlabel')}"
	            					sortable="false" activate="{column3.width=getColumnSize(50)}"
	        						dataField="name" />
	            <mx:DataGridColumn id="column4" headerText="{resourceManager.getString(model.resources.bundleName, 'cost_listlabel')}"
	            					sortable="false" activate="{column4.width=getColumnSize(15)}"
	            					labelFunction="gridFormatterCost"
	            					textAlign="right"  />

        </mx:columns>
    </mx:DataGrid>
</mx:HBox>
