<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
	creationComplete="initPage()" width="100%"
	updateComplete="initPage()">
	
	<mx:Script>
		<![CDATA[
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import no.makingwaves.cust.dss.vo.TravelDeductionVO;
			import mx.collections.ArrayCollection;
            import mx.core.IFlexDisplayObject;
			import no.makingwaves.cust.dss.model.ModelLocator;
			import no.makingwaves.util.Util;
			import mx.managers.PopUpManager;
			import no.makingwaves.cust.dss.view.specifications.travel_deduction;
			
			[Bindable]
			public var model : ModelLocator = ModelLocator.getInstance();
			
			[Bindable]
			public var active_btn_edit : Boolean = true;
			
			[Bindable]
			public var active_btn_delete : Boolean = true;

			private var addWindow:IFlexDisplayObject;
				
			private function initPage():void {
				if (grid_deductions.selectedIndex == -1) {
					this.active_btn_delete = false;
					this.active_btn_edit = false;
				}
				this.initColumnSizes();
			}		
			
			private function getFormattedDateAndTime(date:Date, time:String):String {
				var formattedDate:String = "";
				if (date != null) {
					var dateFormat:String = resourceManager.getString(model.resources.getResourceBundleName(), 'DATE_FORMAT');
					formattedDate = Util.formatDate(date, dateFormat);
					
					if (time != "") 	
						formattedDate += ", kl." + time.substr(0,2) + ":" + time.substr(2,2);
						
				}
				return formattedDate;
			}
			
			public function editSpecification(e:MouseEvent=null):void {
				var popupSpec:travel_deduction = travel_deduction(PopUpManager.createPopUp(this, travel_deduction, true));
				popupSpec.deduction = TravelDeductionVO(grid_deductions.selectedItem);
				popupSpec.addEventListener(CloseEvent.CLOSE, closeWindowHandler);
				
				function closeWindowHandler(e:CloseEvent):void {
					popupSpec.removeEventListener(CloseEvent.CLOSE, closeWindowHandler);
					PopUpManager.removePopUp(popupSpec);
				}
			}
			
			public function deleteSpecification(e:MouseEvent=null):void {
				var traveldeductions:TravelDeductionVO = TravelDeductionVO(grid_deductions.selectedItem); 
				for (var i:Number = 0; i < model.travelDeductionList.length; i++) {
					if (model.travelDeductionList.getItemAt(i) == traveldeductions) {
						model.travelDeductionList.removeItemAt(i);
						break;
					}
				}
			}
			
			public function getSelectedStatus():Boolean {
				return Boolean(this.grid_deductions.selectedIndex != -1);				
			}
			
			private function gridFormatterCost(item:TravelDeductionVO, ref:DataGridColumn):String {
				return item.cost.getCost();
			}
			
			private function gridFormatterDate(item:TravelDeductionVO, ref:DataGridColumn):String {
				var label:String = "";
				label = this.getFormattedDateAndTime(item.date, "");
				return label;
			}
			
			private function dateSortCompareFunction(obj1:Object, obj2:Object):int {
				if (obj1.date > obj2.date) {
					return -1;
				} else if (obj1.date < obj2.date) {
					return 1;
				}
				return 0;
			}
			
			private function gridFormatterDescription(item:TravelDeductionVO, ref:DataGridColumn):String {
				var deductionList:Array = new Array();;
				if (item.breakfast) { deductionList.push(resourceManager.getString(model.resources.bundleName, 'deduction_type_breakfast')); }
				if (item.lunch) { deductionList.push(resourceManager.getString(model.resources.bundleName, 'deduction_type_lunch')); }
				if (item.dinner) { deductionList.push(resourceManager.getString(model.resources.bundleName, 'deduction_type_dinner')); }
				var deductionString:String = deductionList.toString();
				deductionString = Util.searchAndReplace(deductionString, ",", "-");
				return Util.searchAndReplace(deductionString, "-", ", ");
			}
			
			private function initColumnSizes():void {
				var timer:Timer = new Timer(500,1);
				timer.addEventListener(TimerEvent.TIMER_COMPLETE, setColumnSizes);
				timer.start();				
			}
			private function setColumnSizes(e:TimerEvent):void {
				try {
					this.column1.width = getColumnSize(15);
					this.column2.width = getColumnSize(70);		
					this.column3.width = getColumnSize(15);
				} catch (e:Error) { trace("deduction.resumeList.setColumnSizes: " + e.message); }				
			}
			
			private function getColumnSize(percent:Number):Number {
				return (grid_deductions.width * percent) / 100;
			}
			
			private function gridSelectionChanged(e:ListEvent):void {
				if (grid_deductions.selectedIndex != -1) {
					this.active_btn_delete = true;
					this.active_btn_edit = true;
				} else {
					this.active_btn_delete = false;
					this.active_btn_edit = false;
				}
			}
			
		]]>
	</mx:Script>
		
	<mx:DataGrid id="grid_deductions" tabIndex="277" rowCount="{((model.travelDeductionList.length > 0) ? model.travelDeductionList.length : 1)}" width="100%" height="100%" 
				scaleX="{((model.travelDeductionList.length > 0) ? 1 : 0)}" 
				scaleY="{((model.travelDeductionList.length > 0) ? 1 : 0)}" 
				dataProvider="{model.travelDeductionList}" addedToStage="{initColumnSizes()}"
			    change="this.gridSelectionChanged(event)"
				doubleClick="{this.editSpecification(event)}">
				
        <mx:columns>
	        	<mx:DataGridColumn id="column1" headerText="{resourceManager.getString(model.resources.bundleName, 'date_listlabel')}"
	            					sortable="true" sortCompareFunction="dateSortCompareFunction" activate="{column1.width=getColumnSize(15)}"
	            					labelFunction="gridFormatterDate" />
	            <mx:DataGridColumn id="column2" headerText="{resourceManager.getString(model.resources.bundleName, 'deductiontype_listlabel')}"
	            					sortable="false" activate="{column2.width=getColumnSize(70)}"
	            					labelFunction="gridFormatterDescription" />
	            <mx:DataGridColumn id="column3" headerText="{resourceManager.getString(model.resources.bundleName, 'cost_listlabel')}"
	            					sortable="false" activate="{column3.width=getColumnSize(15)}"
	            					labelFunction="gridFormatterCost"
	            					textAlign="right"  />

        </mx:columns>
    </mx:DataGrid>
</mx:HBox>
