<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
	creationComplete="initPage()" width="100%"
	updateComplete="initPage()">
	
	<mx:Script>
		<![CDATA[
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import no.makingwaves.cust.dss.vo.TravelOutlayVO;
			import mx.collections.ArrayCollection;
            import mx.core.IFlexDisplayObject;
			import no.makingwaves.cust.dss.model.ModelLocator;
			import no.makingwaves.util.Util;
			import mx.managers.PopUpManager;
			import no.makingwaves.cust.dss.view.specifications.travel_outlays;
			
			[Bindable]
			public var model : ModelLocator = ModelLocator.getInstance();
			
			[Bindable]
			public var active_btn_edit : Boolean = true;
			
			[Bindable]
			public var active_btn_delete : Boolean = true;

			private var addWindow:IFlexDisplayObject;
				
			private function initPage():void {
				if (this.grid_outlays.selectedIndex == -1) {
					this.active_btn_delete = false;
					this.active_btn_edit = false;
				}
				this.initColumnSizes();
			}		
			
			private function getFormattedDateAndTime(date:Date, time:String):String {
				var formattedDate:String = "";
				if (date != null) {
					var dateFormat:String = resourceManager.getString(model.resources.getResourceBundleName(), 'DATE_FORMAT');
					formattedDate = Util.formatDate(date, dateFormat);
					
					if (time != "") 	
						formattedDate += ", kl." + time.substr(0,2) + ":" + time.substr(2,2);
						
				}
				return formattedDate;
			}
			
			public function editSpecification(e:MouseEvent=null):void {
				var popupSpec:travel_outlays = travel_outlays(PopUpManager.createPopUp(this, travel_outlays, true));
				popupSpec.travel_outlay = TravelOutlayVO(grid_outlays.selectedItem);
				popupSpec.addEventListener(CloseEvent.CLOSE, closeWindowHandler);
				
				function closeWindowHandler(e:CloseEvent):void {
					popupSpec.removeEventListener(CloseEvent.CLOSE, closeWindowHandler);
					PopUpManager.removePopUp(popupSpec);
				}
			}
			
			public function deleteSpecification(e:MouseEvent=null):void {
				var traveloutlays:TravelOutlayVO = TravelOutlayVO(grid_outlays.selectedItem); 
				for (var i:Number = 0; i < model.travelOutlayList.length; i++) {
					if (model.travelOutlayList.getItemAt(i) == traveloutlays) {
						model.travelOutlayList.removeItemAt(i);
						break;
					}
				}
			}
			
			public function getSelectedStatus():Boolean {
				return Boolean(this.grid_outlays.selectedIndex != -1);				
			}
			
			private function gridFormatterCost(item:TravelOutlayVO, ref:DataGridColumn):String {
				return item.cost.getCost();
			}
			
			private function gridFormatterDate(item:TravelOutlayVO, ref:DataGridColumn):String {
				return this.getFormattedDateAndTime(item.date, "");
			}
			
			private function dateSortCompareFunction(obj1:Object, obj2:Object):int {
				if (obj1.date > obj2.date) {
					return -1;
				} else if (obj1.date < obj2.date) {
					return 1;
				}
				return 0;
			}
			
			private function initColumnSizes():void {
				var timer:Timer = new Timer(500,1);
				timer.addEventListener(TimerEvent.TIMER_COMPLETE, setColumnSizes);
				timer.start();				
			}
			private function setColumnSizes(e:TimerEvent):void {
				try {
					this.column1.width = getColumnSize(15);
					this.column2.width = getColumnSize(70);		
					this.column3.width = getColumnSize(15);
				} catch (e:Error) { trace("outlays.resumeList.setColumnSizes: " + e.message); }			
			}
			
			private function getColumnSize(percent:Number):Number {
				return (grid_outlays.width * percent) / 100;
			}
			
			private function gridSelectionChanged(e:ListEvent):void {
				if (grid_outlays.selectedIndex != -1) {
					this.active_btn_delete = true;
					this.active_btn_edit = true;
				} else {
					this.active_btn_delete = false;
					this.active_btn_edit = false;
				}
			}
			
		]]>
	</mx:Script>
		
	<mx:DataGrid id="grid_outlays" tabIndex="272" rowCount="{((model.travelOutlayList.length > 0) ? model.travelOutlayList.length : 1)}" width="100%" height="100%" 
				scaleX="{((model.travelOutlayList.length > 0) ? 1 : 0)}" 
				scaleY="{((model.travelOutlayList.length > 0) ? 1 : 0)}" 
				dataProvider="{model.travelOutlayList}" addedToStage="{initColumnSizes()}"
			    change="this.gridSelectionChanged(event)"
				doubleClick="{this.editSpecification(event)}">
				
        <mx:columns>
	        	<mx:DataGridColumn id="column1" headerText="{resourceManager.getString(model.resources.bundleName, 'date_listlabel')}"
	        					    sortable="true" sortCompareFunction="dateSortCompareFunction" activate="{column1.width=getColumnSize(15)}"	
	            					labelFunction="gridFormatterDate" />
	            					
	            <mx:DataGridColumn id="column2" headerText="{resourceManager.getString(model.resources.bundleName, 'outlaytype_listlabel')}"
	            					sortable="false" activate="{column2.width=getColumnSize(70)}"
	        						dataField="specification" />
	        						
	            <mx:DataGridColumn id="column3" headerText="{resourceManager.getString(model.resources.bundleName, 'cost_listlabel')}"
	            					sortable="false" activate="{column3.width=getColumnSize(15)}"
	            					labelFunction="gridFormatterCost"
	            					textAlign="right"  />

        </mx:columns>
    </mx:DataGrid>
</mx:HBox>
